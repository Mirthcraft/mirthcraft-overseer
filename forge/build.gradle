import net.darkhax.curseforgegradle.UploadArtifact

plugins {
    id 'com.gradleup.shadow'
    id "com.modrinth.minotaur"
    id "net.darkhax.curseforgegradle"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}
repositories {
        maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.eachDependency {
        if (requested.group == "com.google.guava" && requested.name == "guava") {
            useTarget("com.google.guava:guava:33.3.1-jre")
        }
    }

    // Globally exclude errorprone no matter where it comes from
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
}


dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
    modImplementation("de.erdbeerbaerlp:dcintegration.common:$rootProject.core_version")
    forgeRuntimeLibrary("de.erdbeerbaerlp:dcintegration.common:$rootProject.core_version")
    shadowBundle("de.erdbeerbaerlp:dcintegration.common:$rootProject.core_version")

    //Dynmap-API
    modCompileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')


}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    exclude 'com/google/errorprone/**'
    exclude 'META-INF/maven/com.google.errorprone/**'

    // Optional: verify it's not sneaking through other channels
    dependencies {
        exclude(dependency('com.google.errorprone:error_prone_annotations'))
    }

    relocate 'org.slf4j', 'dcshadow.org.slf4j'
}




remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

configurations.all {
    resolutionStrategy.eachDependency {
        if (requested.group == "com.google.guava" && requested.name == "guava") {
            useTarget("com.google.guava:guava:33.3.1-jre")
        }
    }

    // Globally exclude errorprone no matter where it comes from
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
}


modrinth{
    if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        token = project.getProperty("modrinth.apikey") // Use an environment property!
        projectId = 'rbJ7eS5V'
        versionNumber = "${project.mod_version}-${project.minecraft_version}"
        versionName = "DiscordIntegration-Forge ${project.mod_version} (MC ${project.minecraft_version})"
        uploadFile = remapJar
        changelog = project.changelog
        gameVersions = ["${project.minecraft_version}"]
        loaders = ['forge']
        dependencies {
            optional.project "dynmap"
            optional.project "luckperms"
        }
        versionType = project.releaseType.toUpperCase()
    }
}
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiToken = getProperty("curseforge.apikey")

        UploadArtifact mainFile = upload(324952, remapJar)
        mainFile.releaseType = project.releaseType.toLowerCase()
        mainFile.changelog = project.changelog
        mainFile.changelogType = 'markdown'
        mainFile.displayName = "DiscordIntegration-Forge ${project.mod_version} (MC ${project.minecraft_version})"
        mainFile.addModLoader("forge")
        mainFile.addGameVersion("${project.minecraft_version}")

        mainFile.addOptional("dynmapforge")
        mainFile.addOptional("luckperms")

    }
}


tasks.release.dependsOn(tasks.build, publishCurseForge, tasks.modrinth)