architectury {
    common rootProject.enabled_platforms.split(',')
}

repositories {
    maven { url "https://repo.erdbeerbaerlp.de/repository/maven-public/" }
}
configurations {
    shadow
    api.extendsFrom(shadow)
}
shadowJar {
    configurations = [project.configurations.shadow]
    exclude 'com/google/errorprone/**'
    exclude 'META-INF/maven/com.google.errorprone/**'
}
dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation("de.erdbeerbaerlp:dcintegration.common:$rootProject.core_version")
}
artifacts {
    archives tasks.shadowJar
}
configurations.all {
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
}
publishing {
    if (project.hasProperty('nexusUser')) { // $GRADLE_USER_HOME/gradle.properties
        publications {
            maven(MavenPublication) {
                groupId = 'de.erdbeerbaerlp'
                artifactId = 'dcintegration.architectury'
                version = "$rootProject.version-$rootProject.minecraft_version"
                from components.shadow
            }
        }
        repositories {
            maven {
                credentials {// $GRADLE_USER_HOME/gradle.properties
                    username = project.nexusUser
                    password = project.nexusPass
                }
                if(project.version.endsWith('-SNAPSHOT')) {
                    url = "https://repo.erdbeerbaerlp.de/repository/maven-snapshots"
                }else{
                    url = "https://repo.erdbeerbaerlp.de/repository/maven-releases"
                }
            }
        }
    }
}
tasks.release.dependsOn(tasks.build, tasks.publish)