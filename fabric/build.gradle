import net.darkhax.curseforgegradle.UploadArtifact

plugins {
    id 'com.gradleup.shadow'
    id "com.modrinth.minotaur"
    id "net.darkhax.curseforgegradle"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}
repositories {
    mavenCentral()
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.nucleoid.xyz/" }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
    common(project(path: ':fabric-like', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':fabric-like', configuration: 'transformProductionFabric')

    include "de.erdbeerbaerlp:dcintegration.common:$rootProject.core_version"

    include(modImplementation("me.lucko:fabric-permissions-api:$rootProject.fabric_permissions_api"))
    modImplementation include("eu.pb4:placeholder-api:$rootProject.placeholder_api")

    //Styled Chat
    modCompileOnly "maven.modrinth:styled-chat:$rootProject.styled_chat"
    // For embedding a fallback copy (which Shadow will relocate):
    shadowBundle "maven.modrinth:styled-chat:$rootProject.styled_chat"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    // Relocate internal Styled chat for when Mod isn't present externally
    relocate 'eu.pb4.styledchat', 'de.erdbeerbaerlp.dcintegration.shaded.styledchat'
}

remapJar {
    input.set shadowJar.archiveFile
}
modrinth{
    if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        token = project.getProperty("modrinth.apikey") // Use an environment property!
        projectId = 'rbJ7eS5V'
        versionNumber = "${project.mod_version}-${project.minecraft_version}"
        versionName = "DiscordIntegration-Fabric ${project.mod_version} (MC ${project.minecraft_version})"
        uploadFile = remapJar
        changelog = project.changelog
        gameVersions = ["${project.minecraft_version}"]
        loaders = ['fabric']
        dependencies {
            required.project "fabric-api"
            optional.project "vanish"
            optional.project "dynmap"
            optional.project "luckperms"
            optional.project "styled-chat"
        }
        versionType = project.releaseType.toUpperCase()
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiToken = getProperty("curseforge.apikey")

        UploadArtifact mainFile = upload(493679, remapJar)
        mainFile.releaseType = project.releaseType.toLowerCase()
        mainFile.changelog = project.changelog
        mainFile.changelogType = 'markdown'
        mainFile.displayName = "DiscordIntegration-Fabric ${project.mod_version} (MC ${project.minecraft_version})"
        mainFile.addModLoader("fabric")
        mainFile.addGameVersion("${project.minecraft_version}")

        mainFile.addRequirement("fabric-api")
        mainFile.addOptional("vanishmod")
        mainFile.addOptional("dynmapforge")
        mainFile.addOptional("luckperms")
        mainFile.addOptional("styled-chat")

    }
}
tasks.release.dependsOn(tasks.build, publishCurseForge, tasks.modrinth)